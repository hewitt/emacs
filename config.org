#+TITLE: EmacsConfig
#+AUTHOR: Rich Hewitt
#+EMAIL: richard.hewitt@manchester.ac.uk
#+STARTUP: indent
#+PROPERTY: header-args :results silent

To convert this to "./config.el" do M-x org-babel-tangle.

* Notes
+ Originally packages were installed by use-packages ':ensure t'
  mechanism. As of 2022 this is replaced in favour of a NixOS
  Home-Manager configuration that manages most packages.
  
+ I'm using the dev version of Emacs (30.0.50) so 'use-package' (and
  some other things) are built-in, notably 'eglot' and 'treesitter'.
  The mode-line configuration also makes use of
  'mode-line-format-right-align' which needs a recent version.

* Basic setup
** Package archives: ELPA/MELPA
Standard content to configure ELPA and MELPA for packages.

I typically do package provision via NixOS and Home-Manager, but it is
sometimes an interactive interface is useful.

Custom settings points to ~/.emacs.d/custom-settings.el. This is not
controlled by "home-manager" in my NixOS configuration so these custom
settings should be treated as both transient and machine specific.

#+BEGIN_SRC emacs-lisp :tangle yes
  (require 'package)
  (setq package-archives
        '(("GNU ELPA"     . "https://elpa.gnu.org/packages/")
          ("MELPA"        . "https://melpa.org/packages/"))
        package-archive-priorities ; prefer ELPA to MELPA
        '(("GNU ELPA"     . 10)
          ("MELPA"        . 5 )))
  (package-initialize)

  ;; Keep custom settings in a separate file to not pollute this one
  (setq custom-file "~/.emacs.d/custom-settings.el")
  (load custom-file t)
#+END_SRC

** Straight - just in case
Recently a package I rely on had a problem. Rather than revert the
package via NixOS, it was quicker to remove the package from the OS
configuration and just switch to directly installing the correct
revision with 'straight'.

#+BEGIN_SRC emacs-lisp :tangle yes
  (defvar bootstrap-version)
  (let ((bootstrap-file
         (expand-file-name "straight/repos/straight.el/bootstrap.el" user-emacs-directory))
        (bootstrap-version 6))
    (unless (file-exists-p bootstrap-file)
      (with-current-buffer
          (url-retrieve-synchronously
           "https://raw.githubusercontent.com/radian-software/straight.el/develop/install.el"
           'silent 'inhibit-cookies)
        (goto-char (point-max))
        (eval-print-last-sexp)))
    (load bootstrap-file nil 'nomessage))
#+END_SRC

** Change some of the default emacs settings
Some random minor tweaks: scroll bar, tool bar, menu bar, garbage collection.

Note that backups are turned *off*.

#+BEGIN_SRC emacs-lisp :tangle yes
  ;; skip auto backups
  (setq make-backup-files nil)
  ;; (setq backup-directory-alist '(("." . "~/.emacs.d/backups")))

  ;; have mouse input in the terminal -- the disadvantage is you
  ;; need to SHIFT+middle mouse to paste in the terminal
  (xterm-mouse-mode 1)
  ;; Turn off the menu/scroll/toolbar
  (menu-bar-mode -1)
  ;;(scroll-bar-mode -1)
  (tool-bar-mode -1)
  ;; replace annoying yes/no with y/n
  (fset 'yes-or-no-p 'y-or-n-p)
  ;; don't end sentences with a double space
  (setq sentence-end-double-space nil)
  ;; the frequency of garbage collection
  (setq gc-cons-threshold 8000000 ) ; i.e., every ~8MB
  ;; report GC events
  (setq garbage-collection-messages t)
  ;; warn when opening files bigger than 80MB
  (setq large-file-warning-threshold 80000000)
  ;; always follow the symlink
  (setq vc-follow-symlinks t)
#+END_SRC

** Update buffer contents when file changes on "disk"
When editing the same file on a shared drive/remote machine, make sure
that we update any open buffers to show the on-file current status.

#+BEGIN_SRC emacs-lisp :tangle yes
  (global-auto-revert-mode)
#+END_SRC

* Appearance
** Modeline 
This is a simplified home-brew modeline. Set this before calling an
ef-theme since there is a 'ef-themes-post-load-hook' defined below.

#+BEGIN_SRC emacs-lisp :tangle yes
  ;; Prot ef-theme modeline tweak to add box around the modeline
  ;; box is the same colour as background, so looks like a wider mode line.
  (defun my-ef-themes-mode-line ()
    "Tweak the style of the mode lines."
    (ef-themes-with-colors
      (custom-set-faces
       `(mode-line ((,c :background ,bg-mode-line :height 100 :foreground ,fg-main :box (:line-width 6 :color ,bg-mode-line))))
       `(mode-line-inactive ((,c :box (:line-width 1 :color ,bg-active)))))))
  ;; hook to update the colours/style using the above function when theme loaded
  (add-hook 'ef-themes-post-load-hook #'my-ef-themes-mode-line)

  ;; use 'mu' as an external process to get the number of unread email
  ;; the number is a string 'my-email-count-string'
  (defun my/unread-email-command ()
    "Run mu to get how many unread email are in the INBOX"
    (interactive)
    (setq my/email-count-string (substring (shell-command-to-string "mu find date:1w..now maildir:/INBOX flag:unread 2>/dev/null | wc -l") 0 -1)))
  ;; update 'my/email-count-string' every 5 mins with a 10 second delay
  (run-with-timer 0 60 'my/unread-email-command)

  ;; define the line/column information -- fixed 2 character width for columbn
  (setq mode-line-position (list "L%l C%02c"))

  ;; fire symbol for unsaved buffer is selected via (C-x 8 RET)
  (setq-default mode-line-format
                '(
                  (:eval (cond
                          (ryo-modal-mode
                           (propertize " ‚ôå" 'face 'error)) ;; modal indicator
                          (t
                           (propertize " -" 'face 'shadow))))
                  (:eval (if (buffer-modified-p)
                             (propertize "üî• " 'face 'error)
                           (propertize "- " 'face 'shadow)
                           )
                         )
                  ;; if file-truename is "~/a/b/../c/d/filename" then show "a/b/../c/d" in darker colour
                  (:eval (if buffer-file-name  ; not all buffers have a filename (e.g. messages/scratch)
                             (when (mode-line-window-selected-p) 
                               (propertize 
                                (string-join (seq-subseq (split-string buffer-file-truename "/") 1 -1) "/") 
                                'face 'shadow)                                      
                               ) 
                           ) 
                         )
                  ;; ALWAYS show the final filename even if inactive
                  ;; final separator is in usual font
                  "/"
                  ;; filename in a more obvious (warning) colour
                  (:eval (if buffer-file-name  ; not all buffers have a filename (e.g. messages/scratch)
                             (propertize 
                              (string-join (seq-subseq (split-string buffer-file-truename "/") -1 nil)) 
                              'face 'warning)
                           )
                         )
                  ;; everything after here goes on the right .. emacs 30+?
                  mode-line-format-right-align
                  "| "
                  my/email-count-string
                  (:eval (when (mode-line-window-selected-p) 
                           (if (buffer-live-p (get-buffer "*mu4e-main*"))
                               " üì´"
                             "-")))
                  ;; show ONLY the major mode (minor modes are not shown)
                  " | "
                  ;; strip "-Mode" from the end
                  (:eval (when (mode-line-window-selected-p) 
                           (propertize (nth 0
                                            (split-string
                                             (capitalize (symbol-name major-mode)) "-Mode")
                                            )
                                       'face 'success)
                           )
                         )
                  " "
                  (vc-mode vc-mode)
                  " | "
                  mode-line-position        ; show lines and columns as specified above
                  )
                )
#+END_SRC

** Theme 
I've switched to the more systematic themes from Protesilaos Stavrou,
and most recently his 'ef-themes'.

Fonts: The default font is now set via 'custom-settings.el' since it is
machine specific given each has its own DPI settings.

#+BEGIN_SRC emacs-lisp :tangle yes
  ;; Disable all other themes to avoid awkward blending:    
  (use-package ef-themes
    :init
    (mapc #'disable-theme custom-enabled-themes)
    ;; Make customisations that affect Emacs faces BEFORE loading a theme
    ;; (any change needs a theme re-load to take effect).

    (setq ef-themes-to-toggle '(ef-symbiosis ef-frost))
    ;;:config
    ;; Load the theme of choice:
    ;;(load-theme 'ef-summer :no-confirm)
    ;; Light: `ef-day', `ef-light', `ef-spring', `ef-summer'.
    ;; Dark:  `ef-autumn', `ef-dark', `ef-night', `ef-winter'.

    ;; I set the theme at the end of this configuration because of
    ;; some minor issues with code comments showing as underlined [2022]
    )

  ;; DONT add a little bit of transparency
  ;;(set-frame-parameter nil 'alpha-background 100)
  ;;(add-to-list 'default-frame-alist '(alpha-background . 95))

  ;; select a default theme
  (ef-themes-select 'ef-symbiosis)
  #+END_SRC

** Rainbow-delimiters
Colorised brackets to make matching easier.

#+BEGIN_SRC emacs-lisp :tangle yes
  (use-package rainbow-delimiters
    :init
    (message "Use-package: Rainbow delimiters")
    :config
    ;(rainbow-delimiters-mode)
    (add-hook 'prog-mode-hook 'rainbow-delimiters-mode)
    (add-hook 'latex-mode-hook 'rainbow-delimiters-mode))  
#+END_SRC

** Which-key
Pop-up a description of key combinations after a delay.

#+BEGIN_SRC emacs-lisp :tangle yes
  (use-package which-key
    :init 
    (message "Use-package: Which-key mode")
    :config
    (setq which-key-idle-delay 0.25)
    (which-key-mode))
#+END_SRC

* Mode hooks

#+BEGIN_SRC emacs-lisp :tangle yes
  (defun my-display-line-numbers-hook ()
    (display-line-numbers-mode 1))
  ;; latex 
  (add-hook 'latex-mode-hook 'hl-line-mode)
  (add-hook 'latex-mode-hook 'flyspell-mode)
  (add-hook 'latex-mode-hook 'visual-line-mode)
  (add-hook 'latex-mode-hook 'my-display-line-numbers-hook)
  ;; programming
  (add-hook 'prog-mode-hook 'hl-line-mode)
  (add-hook 'prog-mode-hook 'eglot-ensure)
  (add-hook 'prog-mode-hook 'my-display-line-numbers-hook)
  ;; org-mode
  (add-hook 'org-mode-hook 'hl-line-mode)
  (add-hook 'org-mode-hook 'flyspell-mode)
  (add-hook 'org-mode-hook 'visual-line-mode)
#+END_SRC

* Narrowing and completion
** Overview
A useful overview from: https://www.reddit.com/r/emacs/comments/k3c0u7/consult_counselswiper_alternative_for/

The minibuffer completion uses:

+ "completing-read" to define what the completion UI looks like and
  how it behaves.

+ "completing-styles" to define how completion filter/sorts results
  (e.g. does typing "fi fil" match "find-file").

In terms of packages:

+ "icomplete", "fido" and "selectrum" all just define a
  "completing-read" function and implement continuous completion on
  each key press (not technically true for "icomplete" but close
  enough).

+ "Orderless", "Prescient", and the built-in "flex" are
  completion-styles to allow convenient filters like regex, and
  sorting by frequency/recency.

+ "icomplete-vertical" is a minor mode to make "icomplete" vertical.

+ "Consult" is a set of functions to use various Emacs facilities via
  completing-read.

+ "Embark" is a minor mode to allow each minibuffer entry to have
  multiple actions.

All of the above try to use the minibuffer's existing hooks and
extension mechanisms, and benefit from large parts of the rest of
Emacs using those mechanisms too. Consequently, they all interoperate
with each other and other parts of the Emacs ecosystem. You can pick
which you want.

Modes that don't attempt to interoperate (and I avoid):

+ "Ido" performs the same role as "completing-read", but doesn't set
  "completing-read" and so only works for functions that use Ido's own
  completing function. "ido-ubiquitious" sets ido to be
  completing-read. ido appears to be considered somewhat deprecated on
  emacs-devel, in favour of icomplete.

+ "Ivy" doesn't use completing-read at all, and does its own filtering
  (rather than use completion-styles).

+ "Swiper" uses Ivy. I replace with just `C-s`.

+ "Counsel" is a set of functions to use various parts of Emacs via
  minibuffer completion. Very convenient, but only works if you also
  have "Ivy/Swiper". "Consult" is like "Counsel" but uses the built-in
  minibuffer completion.

+ "Helm" doesn't use "completing-read", but does add multiple actions
  on each selection. I would use "embark" if I wanted this
  functionality, but I don't.

** Using standard completing-read interface
- Use 'vertico' as a smaller solution for incremental completion in
  Emacs.

- 'marginalia-mode' adds marginalia to the minibuffer completions.
  Marginalia can only add annotations to be displayed with the
  completion candidates.

- 'consult' provides various practical commands based on the Emacs
  completion function 'completing-read', which allows to quickly select
  an item from a list of candidates with completion. Consult offers in
  particular an advanced buffer switching command 'consult-buffer' to
  switch between buffers and recently opened files. Multiple search
  commands are provided, an asynchronous 'consult-grep',
  'consult-ripgrep' and 'consult-line', which resembles 'swiper'.

- 'corfu' provides in-region (ie. in the buffer) completion candidates
  useful for code-completion when combined with 'eglot' and 'ccls' (see
  the section below). In this config I stick to the terminal mode for
  'corfu' just so it works in both GUI + Terminal modes. Detecting which
  mode we're in and starting the appropriate version is a pain when
  using GUI+Terminal emacsclients connected to a daemon instance.
  
#+BEGIN_SRC emacs-lisp :tangle yes
  (use-package consult
    :init
    (message "Use-package: consult")
    :bind
    ;; see also key-chords elsewhere
    ("C-x b" . consult-buffer)
    ("M-g g" . consult-goto-line)
    ("M-y"   . consult-yank-pop)
    ("C-y"   . yank)
    ("C-s"   . consult-line)
    ("M-g o" . consult-outline))

  (use-package consult-notes
    :commands (consult-notes consult-notes-search-in-all-notes)
    :config
    (consult-notes-denote-mode))

  (use-package vertico
    :custom
    (vertico-cycle t)
    :init
    (message "Use-package: vertico")
    (vertico-mode))

  ;; (code) completion via in-buffer pop-up choices
  (use-package corfu
    :init (message "Use-package: Corfu")
    :custom
    (corfu-cycle t)                ;; Enable cycling for `corfu-next/previous'
    (corfu-auto t)                 ;; Enable auto completion
    (corfu-separator ?\s)          ;; Orderless field separator
    ;; (corfu-quit-at-boundary nil)   ;; Never quit at completion boundary
    ;; (corfu-quit-no-match nil)      ;; Never quit, even if there is no match
    ;; (corfu-preview-current nil)    ;; Disable current candidate preview
    ;; (corfu-preselect 'prompt)      ;; Preselect the prompt
    ;; (corfu-on-exact-match nil)     ;; Configure handling of exact matches
    ;; (corfu-scroll-margin 5)        ;; Use scroll margin
    ;; Enable Corfu only for certain modes.
    :hook ((prog-mode . corfu-mode)
           (latex-mode . corfu-mode)
           (shell-mode . corfu-mode)
           (eshell-mode . corfu-mode))
    ;; Recommended: Enable Corfu globally.
    ;; This is recommended since Dabbrev can be used globally (M-/).
    ;; See also `corfu-exclude-modes'.
    :init
    (setq tab-always-indent 'complete)
    (global-corfu-mode)
    (corfu-prescient-mode))

  (use-package corfu-terminal
    :init
    (message "Use-package: corfu-terminal")
    :config
    ;; let's default to the terminal mode
    (corfu-terminal-mode))

  (use-package prescient
    :init
    (message "Use-package: prescient")
    :config
    ;; you have to set the completion-style(s) to be used
    (setq completion-styles '(substring prescient basic))
    ;; retain completion statistics over restart of emacs
    (prescient-persist-mode))

  (use-package vertico-prescient
    :init
    (message "Use-package: vertico-prescient")
    :config
    (vertico-prescient-mode))

  (use-package corfu-prescient
    :init
    (message "Use-package: corfu-prescient") )

  ;; (use-package orderless
  ;;  :custom (completion-styles '(orderless)))

  (use-package marginalia
    :after vertico
    :custom
    (marginalia-annotators '(marginalia-annotators-heavy marginalia-annotators-light nil))
    :init
    (message "Use-package: marginalia")
    (marginalia-mode))
#+END_SRC

* Interaction
** Splitting window behaviour
Global keys to split the window AND follow by moving point to the new window.

#+BEGIN_SRC emacs-lisp :tangle yes
  ;; move focus when splitting a window
  (defun my/split-and-follow-horizontally ()
    (interactive)
    (split-window-below)
    (balance-windows)
    (other-window 1))
  (global-set-key (kbd "C-x 2") 'my/split-and-follow-horizontally)
  ;; move focus when splitting a window
  (defun my/split-and-follow-vertically ()
    (interactive)
    (split-window-right)
    (balance-windows)
    (other-window 1))
  (global-set-key (kbd "C-x 3") 'my/split-and-follow-vertically)
#+END_SRC

** Modal editing

Roll-your-own-modal editing.

#+BEGIN_SRC emacs-lisp :tangle yes
  ;; edit the init.el configuration file
  (defun my/config-visit ()
    (interactive)
    (find-file "~/CURRENT/NixConfig/outOfStore/.emacs.d/config.org") )

  ;; edit the init.el configuration file
  (defun my/todo-visit ()
    (interactive)
    (find-file "~/Sync/Org/Todo.org") )

  (use-package ryo-modal
    :commands ryo-modal-mode
    :bind ("<escape>" . ryo-modal-mode)
    :after org
    :config
    (ryo-modal-keys
     ;; vi like
     ("."  ryo-modal-repeat)
     ("/"  consult-line)
     ("i"  ryo-modal-mode)
     ;; navigation
     ("h"  backward-char)
     ("j"  next-line)
     ("k"  previous-line)
     ("l"  forward-char)
     ("H"  left-word)
     ("J"  forward-paragraph)
     ("K"  backward-paragraph)
     ("L"  right-word)
     ("b"  consult-buffer)
     ("g"  consult-goto-line)
     ("Y"  consult-yank-pop)
     ("y"  yank)
     ("w"  kill-region)
     ("W"  copy-region-as-kill)
     ;; abbreviated emacs
     ("x" (("s" save-buffer)
           ("f" find-file)
           ("o" other-window)
           ("c" save-buffers-kill-terminal)
           ("0" delete-window)
           ("1" delete-other-windows)
           ("2" my/split-and-follow-horizontally)
           ("3" my/split-and-follow-vertically)))
     ("q" (("a" org-agenda)
           ("d" org-journal-new-entry)
           ("e" my/config-visit)
           ;;("m" mu4e) ; set later after mu4e in mu4e specification section
           ("s" consult-notes-search-in-all-notes)
           ("t" my/todo-visit)
           ("T" org-babel-tangle)
           ("c" org-capture)))
     ;; sugar
     ("["  previous-buffer)
     ("]"  next-buffer)
     )

    (ryo-modal-keys
     ;; First argument to ryo-modal-keys may be a list of keywords.
     ;; These keywords will be applied to all keybindings.
     (:norepeat t)
     ("0" "M-0")
     ("1" "M-1")
     ("2" "M-2")
     ("3" "M-3")
     ("4" "M-4")
     ("5" "M-5")
     ("6" "M-6")
     ("7" "M-7")
     ("8" "M-8")
     ("9" "M-9")))
#+END_SRC

One complication is if we run "emacs -nw" (terminal rather than GUI
interface to emacs) then the "escape" key is interpreted differently
than via Wayland/X11. To deal with this we can use the workaround
employed by xah-fly-keys (or Evil mode too I think).

#+begin_SRC emacs-lisp :tangle yes
  (defvar my/ryo-fast-keyseq-timeout 200)

  (defun my/ryo-tty-ESC-filter (map)
    (if (and (equal (this-single-command-keys) [?\e])
             (sit-for (/ my/ryo-fast-keyseq-timeout 1000.0)))
        [escape] map))

  (defun my/ryo-lookup-key (map key)
    (catch 'found
      (map-keymap (lambda (k b) (if (equal key k) (throw 'found b))) map)))

  (defun my/ryo-catch-tty-ESC ()
    "Setup key mappings of current terminal to turn a tty's ESC into `escape'."
    (when (memq (terminal-live-p (frame-terminal)) '(t pc))
      (let ((esc-binding (my/ryo-lookup-key input-decode-map ?\e)))
        (define-key input-decode-map
          [?\e] `(menu-item "" ,esc-binding :filter my/ryo-tty-ESC-filter)))))

  (my/ryo-catch-tty-ESC)
#+END_SRC

** Scrolling
#+BEGIN_SRC emacs-lisp :tangle yes
  (setq-default scroll-conservatively 20)
  ;; how close to the edge of the buffer does point get when scrolling up/down
  (setq-default scroll-margin 8)

  ;; by default always use pixel...mode.
  (pixel-scroll-precision-mode t)
  (setq pixel-scroll-precision-use-momentum nil)
  (setq pixel-scroll-precision-interpolate-mice t)
  (setq pixel-scroll-precision-large-scroll-height 10.0)
  (setq pixel-scroll-precision-interpolate-page t)

  ;; apply to resizing frames and windows too
  (setq frame-resize-pixelwise t)
  (setq window-resize-pixelwise t)

  ;; define scroll wheel behaviour, including text scaling using C+wheel.
  (setq mouse-wheel-scroll-amount '(0.2 ((shift) . hscroll) ((meta)) ((control meta) . global-text-scale) ((control) . text-scale)))
  (setq mouse-wheel-progressive-speed nil)
  #+END_SRC

** Cut and paste
I use Wayland (no X11), and this interacts with wl-copy.

#+BEGIN_SRC emacs-lisp :tangle yes
  ;; - cut and paste in Wayland environment
  ;; - this puts selected text into the Wayland clipboard
  (setq x-select-enable-clipboard t)
  (defun my/txt-cut-function (text &optional push)
    (with-temp-buffer
      (insert text)
      (call-process-region (point-min) (point-max) "wl-copy" ))
    )
  (setq interprogram-cut-function 'my/txt-cut-function)
#+END_SRC

** Key-chord
Keyboard shortcuts based on double pressing of low-popularity key
combinations (e.g. 'qq'). Key-chord doesn't take account of order
(e.g. 'qa'='aq').

*ISSUES* see: https://github.com/emacsorphanage/key-chord/issues/8

*Disabled as now it is being replaced by ryo-modal*

#+BEGIN_SRC emacs-lisp :tangle no
  ;; rapid-double press to activate key chords
  (use-package key-chord
    ;; Use a specific commit as defined in ~/.emacs.d/straight/versions/general.el
    :straight t
    :init
    (progn
      (message "Use-package: Key-chord" )
      (key-chord-define-global "qs"     'consult-notes-search-in-all-notes) ; search org files
      (key-chord-define-global "qi"     'ibuffer-bs-show) 
      (key-chord-define-global "qw"     'other-window)
      (key-chord-define-global "qt"     'org-babel-tangle)
      (key-chord-define-global "qd"     'org-journal-new-entry)
      (key-chord-define-global "qc"     'org-capture)      
      ;; define some related chords
      (key-chord-define-global "qq"     'consult-buffer)
      (key-chord-define-global "qb"     'consult-bookmark) ; set or jump
      (key-chord-define-global "ql"     'consult-goto-line) )
    :config
    ;; Max time delay between two key presses to be considered a key chord
    (setq key-chord-two-keys-delay 0.1) ; default 0.1
    ;; Max time delay between two presses of the same key to be considered a key chord.
    ;; Should normally be a little longer than `key-chord-two-keys-delay'.
    (setq key-chord-one-key-delay 0.2) ; default 0.2    
    (key-chord-mode 1) )
#+END_SRC

** Editorconfig
Set configuration on a per directory basis via .editorconfig.

#+BEGIN_SRC emacs-lisp :tangle yes
  ;; editorconfig allows specification of tab/space/indent
  (use-package editorconfig
    :init
    (message "Use-package: EditorConfig")
    :config
    (editorconfig-mode 1) )
  
  (setq whitespace-style '(trailing tabs newline tab-mark newline-mark))
#+END_SRC

** Yasnippet
Expand roots to standard text snippets with M-].

#+BEGIN_SRC emacs-lisp :tangle yes
  ;; location of my snippets -- has to go before yas-reload-all
  (setq-default yas-snippet-dirs '("~/.emacs.d/my_snippets"))
  ;; include yansippet and snippets
  (use-package yasnippet
    :init
    (message "Use-package: YASnippet")
    :config
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ;;;; hooks for YASnippet in Latex, C++, elisp & org ;;
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    (add-hook 'c++-mode-hook 'yas-minor-mode)  
    (add-hook 'latex-mode-hook 'yas-minor-mode)
    (add-hook 'emacs-lisp-mode-hook 'yas-minor-mode)
    (add-hook 'org-mode-hook 'yas-minor-mode)
    ;; remove default keybinding
    (define-key yas-minor-mode-map (kbd "<tab>") nil)
    (define-key yas-minor-mode-map (kbd "TAB") nil)
    ;; redefine my own key
    (define-key yas-minor-mode-map (kbd "M-]") yas-maybe-expand)
    ;; remove default keys for navigation
    (define-key yas-keymap [(tab)]       nil)
    (define-key yas-keymap (kbd "TAB")   nil)
    (define-key yas-keymap [(shift tab)] nil)
    (define-key yas-keymap [backtab]     nil)
    ;; redefine my own keys
    (define-key yas-keymap (kbd "M-n") 'yas-next-field-or-maybe-expand)
    (define-key yas-keymap (kbd "M-p") 'yas-prev-field)  
    (yas-reload-all) )
#+END_SRC

* Coding environment
Code completion and on-the-fly check/make.

- interaction with a language back-end is done via 'eglot' which is an
  alternative to lsp-mode. The backend is currently set to 'ccls'.

- To parse appropriate header files requires a 'compile_commands.json'
  file that is consistent with the local machine filesystem.
  
- IN-REGION (ie. buffer) completion is provided by Corfu (Completion
  Overlay Region FUnction). Corfu is configured in the completion
  section above. This provides at-point completion in the main buffer
  rather than via a mini-buffer.

#+BEGIN_SRC emacs-lisp :tangle yes
  ;; eglot is a simpler alternative to LSP-mode
  (use-package eglot
    :init
    (message "Use-package: Eglot")
    (add-hook 'c++-mode-hook 'eglot-ensure)
    (add-hook 'latex-mode-hook 'eglot-ensure) 
    :custom
    (add-to-list 'eglot-server-programs '(c++-mode . ("ccls")))
    (add-to-list 'eglot-server-programs '(latex-mode . ("digestif"))) )

  ;; GIT-GUTTER: SHOW changes relative to git repo
  (use-package git-gutter
    :defer t
    :init
    (message "Use-package: Git-Gutter")
    ;:hook
    ;(prog-mode . git-gutter-mode)
    ;(org-mode . git-gutter-mode)
    )
  ;; activate globally
  (global-git-gutter-mode +1)

  ;; NIX language mode
  (use-package nix-mode
    :mode "\\.nix\\'" ) 
#+END_SRC

** Remap default C++/C major modes to tree-sitter alternatives

#+BEGIN_SRC emacs-lisp :tangle yes
  (add-to-list 'major-mode-remap-alist '(c-mode . c-ts-mode))
  (add-to-list 'major-mode-remap-alist '(c++-mode . c++-ts-mode))
  (add-to-list 'major-mode-remap-alist '(c-or-c++-mode . c-or-c++-ts-mode))
  ;; maximum level of highlighting
  (setq treesit-font-lock-level 4)
#+END_SRC

* Magit
Git interface within emacs.

#+BEGIN_SRC emacs-lisp :tangle yes
  ;; MAGIT
  (use-package magit
    :defer t
    :bind
    ("C-x g" . magit-status)
    :init
    (message "Use-package: Magit installed") )
#+END_SRC

* Org mode
** Basics of Org mode
A fairly standard Org mode configuration. Some minor tweaks to
colourise bold/italic/underline for use with bitmap fonts.

#+BEGIN_SRC  emacs-lisp :tangle yes
  (use-package org
    :init
    (message "Use-package: Org") )

  ;; fancy replace of *** etc
  (use-package org-bullets
    :after org
    :init
    (add-hook 'org-mode-hook 'org-bullets-mode)
    (message "Use-package: Org-bullets") )

  ;; replace emphasis with colors in Org files
  (setq org-emphasis-alist
        '(("*" my/org-emphasis-bold)
          ("/" my/org-emphasis-italic)
          ("_" my/org-emphasis-underline)
          ("=" org-verbatim verbatim)
          ("~" org-code verbatim)
          ("+" (:strike-through t))))

   ;; colorise text instead of changing the font weight.
   (defface my/org-emphasis-bold
     '((default :inherit bold)
       (((class color) (min-colors 88) (background light))
        :foreground "#a60000")
       (((class color) (min-colors 88) (background dark))
        :foreground "#ff8059"))
     "My bold emphasis for Org.")

   (defface my/org-emphasis-italic
     '((default :inherit italic)
       (((class color) (min-colors 88) (background light))
        :foreground "#005e00")
       (((class color) (min-colors 88) (background dark))
        :foreground "#44bc44"))
     "My italic emphasis for Org.")

   (defface my/org-emphasis-underline
     '((default :inherit underline)
       (((class color) (min-colors 88) (background light))
        :foreground "#813e00")
       (((class color) (min-colors 88) (background dark))
        :foreground "#d0bc00"))
     "My underline emphasis for Org.")

   ;; custom capture
   (require 'org-capture)
   ;;(define-key global-map "\C-cc" 'org-capture) ; defined via ryo-modal
   (setq org-capture-templates
         '(
           ("t" "Todo" entry (file+headline "~/Sync/Org/Todo.org" "Inbox")
            "* TODO %?\nSCHEDULED: %(org-insert-time-stamp (org-read-date nil t \"+0d\"))\n%a\n")
           ("z" "Zoom meeting" entry (file+headline "~/Sync/Org/Todo.org" "Meetings")
            "* TODO Zoom, %?\nSCHEDULED: %(org-insert-time-stamp (org-read-date nil t \"+0d\"))\n%i\n"
            :empty-lines 1)) )

   ;; Agenda is constructed from org files in ONE directory
   (setq org-agenda-files '("~/Sync/Org"))

   ;; refile to targets defined by the org-agenda-files list above
   (setq org-refile-targets '((nil :maxlevel . 3)
                              (org-agenda-files :maxlevel . 3)))
   (setq org-outline-path-complete-in-steps nil)         ; Refile in a single go
   (setq org-refile-use-outline-path t)                  ; Show full paths for refiling

   ;; store DONE time in the drawer
   (setq org-log-done (quote time))
   (setq org-log-into-drawer t)

   ;; Ask and store note if rescheduling
   (setq org-log-reschedule (quote note))

   ;; syntax highlight latex in org files
   (setq org-highlight-latex-and-related '(latex script entities))

   ;; define the number of days to show in the agenda
   (setq org-agenda-span 14
         org-agenda-start-on-weekday nil
         org-agenda-start-day "-3d")

   ;; default duration of events
   (setq org-agenda-default-appointment-duration 60)
   (setq org-agenda-prefix-format '(
    ;;;; (agenda  . " %i %-12:c%?-12t% s") ;; file name + org-agenda-entry-type
                                    (agenda  . "  ‚Ä¢  %-12:c%?-12t% s")
                                    (timeline  . "  % s")
                                    (todo  . " %i %-12:c")
                                    (tags  . " %i %-12:c")
                                    (search . " %i %-12:c")))
#+END_SRC

** Org-babel
Reproducible research aide.

#+BEGIN_SRC emacs-lisp :tangle yes
  (use-package gnuplot
    :init
    (message "Use-package: gnuplot for babel installed"))
  ;; languages I work in via babel
  (org-babel-do-load-languages
   'org-babel-load-languages
   '((gnuplot . t) (emacs-lisp . t) (shell . t) (python . t)))
  ;; stop it asking if I'm sure about evaluation
  (setq org-confirm-babel-evaluate nil)

  ;; (defun my-tab-related-stuff ()
  ;;   (setq indent-tabs-mode nil)
  ;;   ;;(setq tab-stop-list (number-sequence 4 200 4))
  ;;   (setq tab-width 2)
  ;;   ;;(setq indent-line-function 'insert-tab) )

  ;; (add-hook 'org-mode-hook 'my-tab-related-stuff)
#+END_SRC

** Denote
This is an Org-roam alternative. It appeals to me because of its
simplicity, focus, spectacular documentation and its from an author
who writes great content.

Searching the Denote files is done via the "consult-notes" package. 

#+BEGIN_SRC emacs-lisp :tangle yes
  (require 'denote)

  ;; Remember to check the doc strings of those variables.
  (setq denote-directory (expand-file-name "~/CURRENT/PNL/Denote/"))
  (setq denote-known-keywords '("research" "admin" "industry" "teaching" "home" "attachment"))
  (setq denote-infer-keywords t)
  (setq denote-sort-keywords t)
  (setq denote-file-type nil) ; Org is the default, set others here
  (setq denote-prompts '(title keywords))

  ;; We allow multi-word keywords by default.  The author's personal
  ;; preference is for single-word keywords for a more rigid workflow.
  (setq denote-allow-multi-word-keywords t)

  (setq denote-date-format nil) ; read doc string

  ;; By default, we fontify backlinks in their bespoke buffer.
  (setq denote-link-fontify-backlinks t)

  ;; Also see `denote-link-backlinks-display-buffer-action' which is a bit
  ;; advanced.

  ;; If you use Markdown or plain text files (Org renders links as buttons
  ;; right away)
  (add-hook 'find-file-hook #'denote-link-buttonize-buffer)

  ;;(require 'denote-dired)
  (setq denote-dired-rename-expert nil)

  (add-hook 'dired-mode-hook #'denote-dired-mode-in-directories)

  ;; Denote does not define any key bindings.  This is for the user to
  ;; decide.  For example:
  (let ((map global-map))
    (define-key map (kbd "C-c n n") #'denote)
    (define-key map (kbd "C-c n N") #'denote-type)
    (define-key map (kbd "C-c n d") #'denote-date)
    (define-key map (kbd "C-c n s") #'denote-subdirectory)
    ;; If you intend to use Denote with a variety of file types, it is
    ;; easier to bind the link-related commands to the `global-map', as
    ;; shown here.  Otherwise follow the same pattern for `org-mode-map',
    ;; `markdown-mode-map', and/or `text-mode-map'.
    (define-key map (kbd "C-c n i") #'denote-link) ; "insert" mnemonic
    (define-key map (kbd "C-c n I") #'denote-link-add-links)
    (define-key map (kbd "C-c n l") #'denote-link-find-file) ; "list" links
    (define-key map (kbd "C-c n b") #'denote-link-backlinks)
    ;; Note that `denote-dired-rename-file' can work from any context, not
    ;; just Dired bufffers.  That is why we bind it here to the
    ;; `global-map'.
    (define-key map (kbd "C-c n r") #'denote-dired-rename-file))

  (with-eval-after-load 'org-capture    
    (setq denote-org-capture-specifiers "%l\n%i\n%?")
    (add-to-list 'org-capture-templates
                 '("n" "New note (with denote.el)" plain
                   (file denote-last-path)
                   #'denote-org-capture
                   :no-save t
                   :immediate-finish nil
                   :kill-buffer t
                   :jump-to-captured t)))

  ;; I still like "org-journal" rather than using "denote".
  (use-package org-journal
    :init
    (message "Use-package: Org-journal")
    :config
    (setq org-journal-dir "~/CURRENT/PNL/JNL/"
          org-journal-date-format "%A, %d %B %Y"
          org-journal-file-format "%Y_%m_%d"
          org-journal-time-prefix "  - "
          org-journal-time-format nil
          org-journal-file-type 'monthly)  )

#+END_SRC 
                 
* PDF tools
This is a great tool if you have to comment on or otherwise annotate
PDFs. The standard method for adding a text comment can be faster
than trying to scribble a hadnwritten note via other methods.

#+BEGIN_SRC emacs-lisp :tangle yes
  ;; pdf tools for organising and annotating PDF
  (use-package pdf-tools
    :config
    (pdf-tools-install) )
#+END_SRC
 
* Email/mu4e
You need the "mu" package and also the executable "mbsync" (the
package that mbsync is in, is usually called "isync"). My existing
workflow was broken by move to Oauth2 in O365. Now I run "davmail" as
an intermediary, with IMAP/SMTP on localhost which seems to run well.
The "davmail" process is started as an asynchronous process under
emacs as needed when 'mu4e' is started.

#+BEGIN_SRC emacs-lisp :tangle yes
  ;; defines mu4e exists, but holds off until needed
  (autoload 'mu4e "mu4e" "Launch mu4e and show the main window" t)

  (ryo-modal-keys
   ("q" (("m" mu4e))))

  ;;
  ;; GETTING new messages
  ;;
  ;; how to get mail
  (setq mu4e-get-mail-command "mbsync Work"
        mu4e-maildir (expand-file-name "~/CURRENT/mbsyncmail")
        mu4e-mu-binary (executable-find "mu"))
  ;; auto GET every 5 mins
  (setq mu4e-update-interval 300)
  ;; to stop mail draft/sent appearing in the recent files list of the dashboard add:
  ;; (add-to-list 'recentf-exclude "\\mbsyncmail\\")

  ;;
  ;; READING and ORGANIZING mail
  ;;
  ;; I don't sync Deleted Items & largely do permanent
  ;;  delete via "D" rather than move to trash via "d" 
  (setq mu4e-trash-folder  "/Trash") 
  ;; [2018] : this stops errors associated with duplicated UIDs -- LEAVE IT HERE!
  (setq mu4e-change-filenames-when-moving t)
  ;; show thread but don't bring back related emails that have been moved
  (setq mu4e-headers-show-thread t
        mu4e-headers-include-related nil
        mu4e-headers-results-limit 200)
  ;; rich text emails are converted using 'shr'
  ;; they are displayed using 'shr-face'
  ;; and for a dark background the 'mu4e' manual suggests:
  (setq shr-color-visible-luminance-min 80)
  ;; ;; show images inline
  ;;(setq mu4e-show-images t)
  ;; use imagemagick, if available
  ;;(when (fboundp 'imagemagick-register-types)
  ;;  (imagemagick-register-types) )
  ;;

  ;; Define what headers to show 
  ;; in the headers list -- a pair of a field
  ;; and its width, with `nil' meaning 'unlimited'
  ;; best to only use nil for the last field.
  (setq mu4e-headers-fields
        '((:human-date          .  10)   ;; alternatively, use :date
          (:flags               .   5)
          (:recipnum            .   3)
          (:from-or-to          .  30)
          (:thread-subject      . nil))  ;; alternatively, use :thread-subject
        )
  ;; shortcut keys are used in the main-view
  (setq mu4e-maildir-shortcuts
        '( ("/INBOX"          . ?i)
           ("/Sent"           . ?s)
           ("/Trash"          . ?t)
           ("/Drafts"         . ?d)
           ("/BULK"           . ?b)))
  ;; bookmarks
  (setq mu4e-bookmarks
        ' ((:name "Unread" :query "flag:unread AND NOT flag:trashed AND NOT maildir:/JUNK" :key 117) ; bu
           (:name "Today" :query "date:today..now" :key 116)                   ; bt
           (:name "Week" :query "date:7d..now" :hide-unread t :key 119)        ; bw
           (:name "Attachment" :query "flag:a" :key 97)                        ; ba
           (:name "Flagged"    :query "flag:F" :key 102)                       ; bf
           ))       
  ;; don't auto update in the headers view, wait for return to main view
  (setq mu4e-headers-auto-update nil) 

  ;; Couple to Org -- not sure if this is strictly required or not?
  (require 'mu4e-org)

  ;;
  ;; SENDING and COMPOSING
  ;;
  ;; configure for msmtp as this is easy to test from the CLI
  (setq send-mail-function 'sendmail-send-it
        sendmail-program "msmtp"
        mail-specify-envelope-from t
        message-sendmail-envelope-from 'header
        mail-envelope-from 'header)
  ;; Note: sent mails should appear in O365 sent list
  ;; O365 uses "Sent Items" in the web interface but this
  ;; appears as just "Sent" with mbsync set to "Patterns *"
  (setq mu4e-sent-folder   "/Sent")
  ;; don't keep message buffers around
  (setq message-kill-buffer-on-exit t)
  ;; general emacs mail settings; used when composing e-mail
  ;; the non-mu4e-* stuff is inherited from emacs/message-mode
  (setq mu4e-reply-to-address "richard.hewitt@manchester.ac.uk"
        user-mail-address "richard.hewitt@manchester.ac.uk"
        user-full-name  "Rich Hewitt")
  ;; sent messages are copied into the 'mu4e-sent-folder' defined above
  ;; Make sure that .davmail.properties has .smtpSaveInSent=false otherwise we get
  ;; 2 copies in the O365 "Sent Items" folder
  (setq mu4e-sent-messages-behavior 'sent)
  ;; compose signature
  (setq message-signature-file "~/CURRENT/dot.signature")
  (setq mu4e-compose-signature-auto-include t)
  ;; don't wrap at 70-something columns
  (setq mu4e-compose-format-flowed t)
  ;; define where to put draft email
  (setq mu4e-drafts-folder "/Drafts")
  ;; spell check during compose
  (add-hook 'mu4e-compose-mode-hook
            (defun my/do-compose-stuff ()
              "My settings for message composition."
              ;;(set-fill-column 72)
              (flyspell-mode)
              ;; turn off autosave, otherwise we end up with multiple
              ;; versions of sent/draft mail being sync'd
              (auto-save-mode -1)))


#+END_SRC

We need some quick elisp to start 'davmail' when 'mu4e' starts in order to connect
to O365 with MFA. We do a bit of a hack to stop the 'davmail' process with a timer
check every 15 minutes to see if 'mu4e' is still running or not.

#+BEGIN_SRC emacs-lisp :tangle yes
  (defun my/davmail-start ()
    "Start davmail process for mu4e."
    (interactive)
    (if (get-process "davmail") ; look for the started process 
        (message "[debug] davmail process already running for mu4e") ; don't start more than one davmail process
      (let ((default-directory "~/"))
        (start-process "davmail" "*davmail*" "~/.nix-profile/bin/davmail" "-server"))))

  (defun my/davmail-stop ()
    "Stop davmain if mu4e is not active."
    (interactive)
    ;; check if mu4e-main buffer is present as a proxy for mu4e running
    ;; the 'mu4e-running-p' function will only be available IF I've started mu4e
    (if (buffer-live-p (get-buffer "*mu4e-main*"))
        ;; mu4e IS running so DONT stop davmail
        (message "[debug] mu4e still active, not stopping davmail process")
      ;; mu4e is NOT running so try to kill davmail ONLY IF it is running
      (if (process-status "davmail")
          (kill-process "davmail"))))

  ;; start davmail when entering mu4e
  (add-hook 'mu4e-main-mode-hook 'my/davmail-start)

  ;; I can't find any suitable exit hooks in mu4e so a quick hack is to
  ;; stop 'davmail' every 5 mins if mu4e is not active
  (run-with-timer 0 (* 5 60) 'my/davmail-stop)
#+END_SRC

* AGE encryption
'AGE' is a (arguably) more modern and simpler
replacement for the standard GPG applications.

#+BEGIN_SRC emacs-lisp :tangle yes
  (use-package age
    :demand
    :custom
    (age-program "rage")   ; 'rage' is the rust implementation of 'age' that supports pinentry
    (age-default-identity "~/CURRENT/AGE/yubikey-bb978fd1-identity.txt")
    (age-default-recipient
     '("~/CURRENT/AGE/recovery-recipient.pub"            ; cold-storage recovery
       "~/CURRENT/AGE/yubikey-bb978fd1-recipient.pub"))  ; active hardware key
    :config
    (setq age-armor nil) ;; don't convert to ASCII so I can see multiple key headers from the CLI
    (age-file-enable))

  (straight-use-package
   '(passage :type git :host github :repo "anticomputer/passage.el"))
  (require 'passage)

#+END_SRC

* Wrap up
** Add custom file extensions to set major modes
I use "m4" to update headers/footers/dates in LaTeX files for leccture
course material. So here we default to latex-mode for .m4 extensions
too. In addition ".gnu" for Gnuplot, ".m" for Octave and ".nix" for
NixOS.

#+BEGIN_SRC emacs-lisp :tangle yes
  ;; setup files ending in ‚Äú.m4‚Äù to open in LaTeX-mode
  ;; for use in lecture note construction
  (add-to-list 'auto-mode-alist '("\\.m4\\'" . latex-mode))
  ;; my default gnuplot extension
  (add-to-list 'auto-mode-alist '("\\.gnu\\'" . gnuplot-mode))
  ;; Octave/Matlab
  (add-to-list 'auto-mode-alist '("\\.m\\'" . octave-mode))
  ;; Nix language
  (add-to-list 'auto-mode-alist '("\\.nix\\'" . nix-mode))
#+END_SRC
